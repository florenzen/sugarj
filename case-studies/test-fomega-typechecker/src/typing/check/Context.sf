module typing.check.Context

import org.sugarj.languages.Fomega

import typing.check.Unknowns

signature
  sorts Context

  constructors
    CtxEmpty: Context
    CtxBindTVar: Context * FomegaID * FomegaKind -> Context
    CtxBindTVarDef: Context * FomegaID * FomegaKind * FomegaType -> Context
    CtxBindVar: Context * FomegaID * FomegaType -> Context

rules
  dom: CtxEmpty() -> []
  dom: CtxBindTVar(ctx,x,K) -> [x | <dom> ctx]
  dom: CtxBindTVarDef(ctx,x,K,T) -> [x | <dom> ctx]
  dom: CtxBindVar(ctx,x,T) -> [x | <dom> ctx]

  freeIn: (x,ctx) -> <not(elem)> (x,xs) where <dom> ctx => xs

  lookup-expr-var : (x, CtxEmpty()) -> (TyUnknown(), [(x, <concat-strings> ["undefined identifier ", x])])
  lookup-expr-var : (x, CtxBindTVar(ctx, Y, K)) -> <lookup-expr-var> (x, ctx)
  lookup-expr-var : (x, CtxBindTVarDef(ctx, Y, K, T)) -> <lookup-expr-var> (x, ctx)
  lookup-expr-var : (x, CtxBindVar(ctx, y, T)) -> (T, [])
    where <structurally-equal> (x, y)
  lookup-expr-var : (x, CtxBindVar(ctx, y, T)) -> <lookup-expr-var> (x, ctx)
    where <not(structurally-equal)> (y, x) 
