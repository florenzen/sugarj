package template;

import org.sugarj.languages.Stratego;

import template.Core;
import template.ConcreteSyntax;
import template.util.MetaExplode;

public sugar UnquoteSplicing { 
  context-free syntax
    UnquoteSplicing -> JavaImportDec
    UnquoteSplicing -> JavaClassBodyDec
    UnquoteSplicing -> JavaBlockStm
    UnquoteSplicing -> JavaFormalParam
    UnquoteSplicing -> JavaActualTypeArg

  context-free syntax
    "$*{" UnquoteSplice "}" -> UnquoteSplicing {cons("FromMetaExpr")}
    StrategoStrategy -> UnquoteSplice {cons("UnquoteSplice")}

  desugarings
    list-unquote-desugar

  rules
    list-unquote-desugar :
      SDefNoArgs(s, body) -> 
      SDefNoArgs(s, Seq(Match(Var(modelvar)), body'))
    where
      <oncetd(?UnquoteSplice(_))> body;
      modelvar := "this__";
      <topdown(try(list-unquoted-app(|modelvar)))> body => body'

    unquote-desugar :
      RuleNoCond(t1, t2) -> 
      RuleNoCond(As(Var(modelvar), t1), t2')
    where
      <oncetd(?UnquoteSplice(_))> t2;
      modelvar := "this__";
      <topdown(try(list-unquoted-app(|modelvar)))> t2 => t2'

    unquote-desugar :
      Rule(t1, t2, conds) -> 
      Rule(As(Var(modelvar), t1), t2', conds')
    where
      <oncetd(?UnquoteSplice(_))> t2;
      modelvar := "this__";
      <topdown(try(list-unquoted-app(|modelvar)))> t2 => t2';
      <topdown(try(list-unquoted-app(|modelvar)))> conds => conds'

    list-unquoted-app(|modelvar) :
      Op("Cons",[UnquoteSplice(strat), xs]) ->
      App(CallNoArgs(SVar("conc")), NoAnnoList(Tuple([strat-app, xs])))
    where strat-app := App(strat, Var(modelvar))
}